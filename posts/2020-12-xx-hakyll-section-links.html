# Adding section links to articles with Hakyll

It is handy to be able to link to section headings in long articles.
Pandoc and Hakyll don't do that out of the box.  But they give you
all the power you need to implement it yourself.  In this post I'll
show you how.


## Objective

The primary objective is to provide links (HTML `<a>` element) to
section headings.  They should be located near or within the heading
element.  Having them in the document will make it easy for readers
to grab a link to a specific section of the article.  (I myself
often want to do this!)

While you could make the heading itself a link, I like the approach
that reveals a link when you hover the pointer on the heading.  Some
sites use a pilcrow (Â¶), pound (#) or a link symbol (ðŸ”—) as the link
text.  I will use a section sign (Â§).


## Building blocks

Pandoc does set the `id` attribute of HTML heading elements to a
value derived from the heading text.  For example, one of my
previous posts had a section headed [Probabilities][].  The HTML
for the heading is:

```html
<h2 id="probabilities">Probabilities</h2>
```

[Probabilities]: 2020-03-31-quickcheck-hedgehog.html#probabilities

The value of the `id` attribute will be the `href` target of the
`<a>` element we create (with `#` prepended to make it a URI
fragment).

Hakyll provides the `pandocCompilerWithTransform` function for
compiling documents using Pandoc and applying an arbitrary
transformation to it.  It's type is:

```haskell
pandocCompilerWithTransform
  :: ReaderOptions
  -> WriterOptions
  -> (Pandoc -> Pandoc)
  -> Compiler (Item String)
```

Note the `(Pandoc -> Pandoc)` argument.  This is the tranformation
function.  It works with the [Pandoc][] AST, rather than HTML or the
input type (e.g. Markdown).

[Pandoc]: https://hackage.haskell.org/package/pandoc-types-1.21/docs/Text-Pandoc-Definition.html#t:Pandoc

For constructing such a transformation, Pandoc provides the
`[Text.Pandoc.Walk][]` module and the `walk` function:

```haskell
walk :: (Walkable a b) => (a -> a) -> b -> b
```

`walk f x` walks the structure x (bottom up) and replaces every
occurrance of an `a` with the result of applying `f` to it.

There are many instances of `Walkable`.  We are interested in the
one that visits all the `Block` elements (that's what headers are)
in the `Pandoc`:

```haskell
instance Walkable Block Pandoc
```

[Text.Pandoc.Walk]: https://hackage.haskell.org/package/pandoc-types-1.21/docs/Text-Pandoc-Walk.html#v:walk


## Putting it together

I needed a handful of Pandoc constructors to implement the
transformation.  For the `Block` data type I only needed `Header`.
It represents a document header with `Int` level (`1` is highest),
attributes, and the list of `Inline` elements that constitute the
header content.

```haskell
data Block
  ...
  | Header Int {- level -} Attr [Inline]
  ...
```

I also had to construct a `Link` (one of the cases of the `Inline`
data type).  A `Link` has attributes, content (another `[Inline]`)
and a target.  I also used the `Str` and `Space` constructors.

```haskell
data Inline
    = Str Text
    ...
    | Space                 -- ^ Inter-word space
    ...
    | Link Attr [Inline] Target
```

By the way, `Attr` and `Target` are defined as:

```haskell
-- id, classes and key-value pairs
type Attr = (Text, [Text], [(Text, Text)])

-- URI, title
type Target = (Text, Text)
```

With these constructors in hand, here is the whole transformation
function:

```haskell
addSectionLinks :: Pandoc -> Pandoc
addSectionLinks = walk f where
  f (Header n attr@(idAttr, _, _) inlines) | n > 1 =
      let link = Link nullAttr [Str "Â§"] ("#" <> idAttr, "")
      in Header n attr (inlines <> [Space, link])
  f x = x
```

Note that we only apply this change to headers of depth greater than
one.  I did not see a need to provide a link for the article title,
which is at the top.  For all other headers, we add a `Link` to its
inline content, where the target is the fragment pointing at the
`idAddr` of the header itself.

To apply the transformation, I had to replace a single occurrence
of:

```haskell
pandocCompiler :: Compiler (Item String)
```

with:

```haskell
pandocCompilerWithTransform
        defaultHakyllReaderOptions
        defaultHakyllWriterOptions
        addSectionLinks
```


## Style

My objective was to hide the header link until the cursor is upon
the header.  This was accomplished with a small dose of CSS:

```css
h2 a, h3 a, h4 a, h5 a, h6 a {
    text-decoration: none;
    color: grey;
    visibility: hidden;
}

h2:hover a, h3:hover a, h4:hover a, h5:hover a, h6:hover a {
  visibility: visible;
}
```

I also used `text-decoration` and `color` to make the link
appearance clean and understated.


## Conclusion

Not much to say, really.  Pandoc is still awesome.  Hakyll is still
awesome.  And I am very happy with the results of this little
enhancement.  Go forth and pilcrow-ise your Hakyll sites!
